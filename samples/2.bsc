10   REM Sort values of string array
20   PRINT "Sorting values of string array"
30   INPUT "Enter array size: ", N
40   DIM A$(N - 1)
50   PRINT "Enter strings to sort:"
60   FOR i = 1 TO N
70   PRINT i;
80   INPUT A$(i - 1)
90   NEXT i
100  GOSUB 1000
110  GOSUB 2000
120  INPUT "Press <Enter> to quit...", s$
130  END

1000 REM Sort array A$ of size 0 .. N - 1
1010 FOR i = 0 TO N - 2
1020 m$ = A$(i)
1030 mi = i
1040 FOR j = i + 1 TO N - 1
1050 IF m$ <= A$(j) THEN 1080
1060 m$ = A$(j)
1070 mi = j
1080 NEXT j
1090 A$(mi) = A$(i)
1100 A$(i) = m$
1110 NEXT i
1120 RETURN

2000 REM Print array A$ of size 0 .. N - 1
2010 FOR i = 0 TO N - 1
2020 PRINT A$(i)
2030 NEXT i
2040 RETURN
