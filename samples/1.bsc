10   REM Sort values of numeric array
20   REM Copyright (c) 2020 Nikolay Pletnev
30   REM MIT license

40   PRINT "Sorting values of numeric array"
50   INPUT "Enter array size: ", N
60   DIM A(N - 1)
70   PRINT "Enter numbers to sort:"
80   FOR i = 1 TO N
90   PRINT i;
100  INPUT A(i - 1)
110  NEXT i
120  GOSUB 1000
130  GOSUB 2000
140  INPUT "Press <Enter> to quit...", s$
150  END

1000 REM Sort array A of size 0 .. N - 1
1010 FOR i = 0 TO N - 2
1020 m = A(i)
1030 mi = i
1040 FOR j = i + 1 TO N - 1
1050 IF m <= A(j) THEN 1080
1060 m = A(j)
1070 mi = j
1080 NEXT j
1090 A(mi) = A(i)
1100 A(i) = m
1110 NEXT i
1120 RETURN

2000 REM Print array A of size 0 .. N - 1
2010 FOR i = 0 TO N - 1
2020 PRINT A(i),
2030 NEXT i
2040 PRINT
2050 RETURN